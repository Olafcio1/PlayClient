import java.nio.charset.StandardCharsets
import java.nio.file.FileVisitResult
import java.nio.file.Files
import java.nio.file.Path
import java.nio.file.SimpleFileVisitor
import java.nio.file.StandardCopyOption
import java.nio.file.attribute.BasicFileAttributes

plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    accessWidenerPath.set(projectDir.toPath().resolve("src/main/resources/playclient.accesswidener").toFile())
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

repositories {
    maven {
        name = "meteor-maven"
        url = uri("https://maven.meteordev.org/releases")
    }
    maven {
        name = "meteor-maven-snapshots"
        url = uri("https://maven.meteordev.org/snapshots")
    }
}

dependencies {
    // Main
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Mods
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "meteordevelopment:meteor-client:${project.minecraft_version}-SNAPSHOT"
}

String getLatestCommit() {
    var proc = new ProcessBuilder()
            .command("git", "log", "-n", "1")
            .directory(projectDir)
            .inheritIO()
    .start()
    proc.waitFor()
    return proc.inputStream.getText("UTF-8").split("\n")[0].split(" ")[1]
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "gh_hash": getLatestCommit()
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

static def deleteAll(Path directory) {
    Files.walkFileTree(directory, new SimpleFileVisitor<Path>() {
        @Override
        FileVisitResult visitFile(Path file, BasicFileAttributes attrs) throws IOException {
            Files.delete(file)
            return FileVisitResult.CONTINUE
        }

        @Override
        FileVisitResult postVisitDirectory(Path dir, IOException e) throws IOException {
            if (e == null) {
                Files.delete(dir)
                return FileVisitResult.CONTINUE
            } else {
                // directory iteration failed
                throw e
            }
        }
    })
}

tasks.register("make") {
    dependsOn "build"
    doLast {
        var bin = projectDir.toPath().resolve("./bin")
        var libs = projectDir.toPath().resolve("./build/libs")

        deleteAll(bin)

        var fn = "%s-%s.jar".formatted(archives_base_name, version)

        Files.createDirectory(bin)
        Files.move(
                libs.resolve(fn),
                bin.resolve(fn)
        )
        Files.copy(
                bin.resolve(fn),
                bin.resolve("%s-nightly.jar".formatted(archives_base_name))
        )

        new ProcessBuilder()
                .command("git", "add", "-f", fn)
                .inheritIO()
                .directory(bin.toFile())
        .start().waitFor()

        var readme = projectDir.toPath().resolve("README.md")
        var readmeEnc = StandardCharsets.UTF_8
        var readmeText = Files.readString(readme, readmeEnc)

        readmeText = readmeText.replaceAll('/heads/main/bin/.+?\\.jar">\n', '/heads/main/bin/%s">\n'.formatted(fn))

        Files.writeString(readme, readmeText, readmeEnc)
    }
}

tasks.register("snap") {
    dependsOn "build"
    doLast {
        var bin = projectDir.toPath().resolve("./bin")
        var libs = projectDir.toPath().resolve("./build/libs")

        var fn = "%s-%s.jar".formatted(archives_base_name, version)

        Files.move(
                libs.resolve(fn),
                bin.resolve("%s-nightly.jar".formatted(archives_base_name)),
                StandardCopyOption.REPLACE_EXISTING
        )

        new ProcessBuilder()
                .command("git", "add", "-f", fn)
                .inheritIO()
                .directory(bin.toFile())
        .start().waitFor()
    }
}
